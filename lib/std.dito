# ---------------------------------------------------------------------
# std.dito :
# file contains a super basic standard lib to extend the capabilities
# of the built in functions and basic types. This will be split into
# individual modules in the future as the language and features grow.
# To some extent this file is being used as a implementation playground.
# use:
#     import std
# ---------------------------------------------------------------------

# ---------------------------------------------------------------------
# constants:

let PI      =  3.141592653589793
let E       =  2.718281828459045

# TODO: trailing comments.
# MIN_INT 64bit: -9223372036854775808
# MAX_INT 64bit:  9223372036854775807

let MIN_INT = (1 << 63)

let MAX_INT = (1 << 63) - 1


# ---------------------------------------------------------------------
# Array ops:

let last   = func(it) -> it[len(it) - 1]
let first  = func(it) -> it[0]
let append = func(it, val) ->  it ++ [val]
let copy   = func(it) -> [] ++ it
# let pop    = func(it) -> TODO: implement slicing.

func map(fn, it) {
    let mut i = 0
    let arr = copy(it)
    for v in it {
        arr[i] = fn(v)
        i += 1
    }
    return arr
}

func reduce(fn, it) {
    let mut val = it[0]
    for i in range(1, len(it)) {
        val = fn(val, it[i])
    }
    return val
}

func filter(fn, it) {
    let mut new_it = []
    for v in it {
        if fn(v) {
            # this will fail with strings.
            new_it = new_it ++ [v]
        }
    }
    return new_it
}

func reversed(it) {
    let arr = copy(it)
    let mut i = 0
    let mut j = len(it) - 1
    for i < j {
        swap(arr, i, j)
        i += 1
        j -= 1
    }
    return arr
}

# perform inplace swap of items.
func swap(arr, i, j) {
    let tmp = arr[i]
    arr[i] = arr[j]
    arr[j] = tmp
    return arr
}

func join(it, sep) {
    # join items in an Iter as strings
    let mut val = string(it[0])
    for i in range(1, len(it)) {
        val = val ++ sep ++ string(it[i])
    }
    return val
}

func split(it, sep) {
    # split a string by seperator. Not really that effiecent.
    # maybe need some better language constructs for this.
    let mut arr = []
    let mut tmp = ""
    for c in it {
        if string(c) == sep {
            if tmp != "" {
                arr = arr ++ [tmp]
                tmp = ""
            }
        } else {
            tmp = tmp ++ string(c)
        }
    }
    if tmp != "" {
        arr = arr ++ [tmp]
    }
    return arr
}

func repeat(str, n) {
    let mut s = ""
    for _ in range(0, n) {
        s = s ++ str
    }
    return s
}

# ---------------------------------------------------------------------
# math :
#

let add     = func(a, b)  -> a + b
let sub     = func(a, b)  -> a - b
let div     = func(a, b)  -> a / b
let mul     = func(a, b)  -> a * b
let idiv    = func(a, b)  -> a // b
let mod     = func(a, b)  -> a % b
let even    = func(a)     -> a % 2 == 0
let odd     = func(a)     -> a % 2 != 0
let sqrt    = func(a)     -> a**0.5
let exp     = func(x)     -> E**x
let divmod  = func(a, b)  -> [a/b, a%b]
let hypot   = func(a, b)  -> (a**2 + b**2)**0.5
let sum     = func(it)    -> reduce(add, it)
let avg     = func(it)    -> sum(it) / len(it)
let prod    = func(it)    -> reduce(mul, it)
let pow     = func(a,b,z) -> a**b % z
let min     = func(a, b)  -> a if a < b else b
let max     = func(a, b)  -> a if a > b else b
let argmax  = func(f,a,b) -> a if f(a, b) else b
let floor   = func(a)     -> int(a)
let ceil    = func(a)     -> int(a) + 1
let radians = func(a)     -> a * 57.295779513
let degrees = func(a)     -> a * 0.01745329252
let fact    = func(a)     -> prod(range(1, a + 1))


# ---------------------------------------------------------------------
# random :
# Builds on top of the built in function `rand`.

# return random int where result <= a
let randint = func(a) -> int(rand() * a)

# return a random choice from a iterable object.
let choice  = func(it) -> it[int(rand() * len(it))]

# generate a random number between a and b.
let uniform = func(a, b) -> (a + ((abs(a) if a<0 else 0) + b) * rand())

func shuffled(it) {
    let arr = copy(it)
    let maxIndex = len(arr) - 1
    for i in range(0, maxIndex) {
        swap(arr, i, randint(maxIndex))
    }
    return arr
}

# ---------------------------------------------------------------------
# time :

let UNIX_EPOCH = 0x019DB1DED53E8000
let UNIX_DAY   = 86400
let UNIX_HOUR  = 3600
let UNIX_MIN   = 60

# return a string representation of the current time.
func strtime(t) {
    let mut secs = (t - UNIX_EPOCH) % UNIX_DAY
    if secs < 0 {
        secs += UNIX_DAY
    }
    let tm_hour = secs // 3600
    let tm_min  = secs // 60 % 60
    let tm_sec  = secs % 60
    return join([tm_hour, tm_min, tm_sec], ":")
}